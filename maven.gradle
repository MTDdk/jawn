apply plugin: 'maven-publish'
apply plugin: 'signing'

// maven metadata
group = 'net.javapla.jawn'

// Collect the source
/*task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

// Generates the javadoc
task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}*/

java {
  withJavadocJar()
  withSourcesJar()
}

ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

publishing {
  publications {
    mavenJava(MavenPublication) {
    
      from components.java
      
      //artifact sourcesJar
      //artifact javadocJar
      
      
      pom {
        name = "${project.group}:${rootProject.name}"
        description = 'jawn - A simple web framework in Java'
        url = 'https://github.com/MTDdk/jawn'
        
        licenses {
          license {
            name = 'GNU LGPL, Version 3.0'
            url = 'http://www.gnu.org/licenses/lgpl-3.0.txt'
          }
        }
        
        developers {
          developer {
            id = 'MTDdk'
            name = 'MTDdk'
            email = 'alvn@alvn.dk'
          }
        }
        
        issueManagement {
          system = 'Github Issues'
          url = 'https://github.com/MTDdk/jawn/issues'
        }
        
        scm {
          connection = 'scm:git://github.com/MTDdk/jawn.git'
          developerConnection = 'scm:git:git@github.com:MTDdk/jawn.git'
          url = 'https://github.com/MTDdk/jawn'
        }
      }
      
      
      repositories {
        maven {
          def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
          def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
          url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
        
        
          gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.hasTask(':generatePomFileForMavenJavaPublication') && !taskGraph.hasTask(':publishToMavenLocal')) {
            
              // only do any maven related if we have the needed properties available
              if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                credentials {
                  username = ossrhUsername
                  password = ossrhPassword
                }
              } else {
                throw new GradleException('You do not have your credentials stated')
              }
            }
          }

          
        }
      }
      
    }
  }
}



signing {
  required { isReleaseVersion }
  sign publishing.publications.mavenJava
}

javadoc {
  enabled = true
  
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
  
  failOnError = false
}